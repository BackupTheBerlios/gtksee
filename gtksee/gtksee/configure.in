dnl Process this file with autoconf to produce a configure script.

AC_INIT(src/gtksee.c)
AM_INIT_AUTOMAKE(gtksee, 0.5.4)
AM_CONFIG_HEADER(config.h)

dnl
dnl Checks for programs.
dnl ====================
AC_PROG_CC
AC_ISC_POSIX
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_RANLIB

CFLAGS=""
IMCFLAGS=""
IMLIBS=""

dnl
dnl Only use -Wall if we have gcc
dnl =============================
if test "x$GCC" = "xyes"; then
  if test -z "`echo "$CFLAGS" | grep "\-Wall" 2> /dev/null`" ; then
    CFLAGS="$CFLAGS -Wall"
  fi
fi

dnl
dnl Specify paths to look for image libraries
dnl =========================================
AC_ARG_WITH(im-libs,
	[  --with-im-libs          pass compiler flags to look for libraries],
	[IMLIBS="$IMLIBS -L$withval"])

AC_ARG_WITH(im-incs,
	[  --with-im-incs          pass compiler flags to look for header files],
	[IMCFLAGS="$IMCFLAGS -I$withval"])

dnl
dnl Checks for image libraries.
dnl ===========================
AC_CHECK_LIB(m, pow, IMLIBS="$IMLIBS -lm")
AC_CHECK_LIB(z, gzread, IMLIBS="-lz $IMLIBS",
	AC_CHECK_LIB(gz, gzread, IMLIBS="-lgz $IMLIBS"))

jpeg_support="no (libjpeg required)"

AC_CHECK_LIB(jpeg, jpeg_read_header,
	[jpeg_support="yes"
	AC_DEFINE(HAVE_LIBJPEG)
	IMLIBS="-ljpeg $IMLIBS"],,$IMCFLAGS $IMLIBS)

tiff="no"
tiff_support="no (libtiff required)"

AC_CHECK_LIB(tiff, TIFFGetVersion,
 	tiff="tiff",
	AC_CHECK_LIB(tiff34, TIFFGetVersion,
	tiff="tiff34",,$IMCFLAGS $IMLIBS)
	,$IMCFLAGS $IMLIBS)

if test "$tiff" != "no"; then
	tiff_support="yes"
        AC_DEFINE(HAVE_LIBTIFF)
        IMLIBS="-l$tiff $IMLIBS"
fi

png_support="no (libpng required)"

AC_CHECK_LIB(png, png_read_info,
	[png_support="yes"
	AC_DEFINE(HAVE_LIBPNG)
	IMLIBS="-lpng $IMLIBS"],,$IMCFLAGS $IMLIBS)

dnl
dnl Checks for header file os2.h and socket library
dnl ===============================================
AC_CHECK_HEADERS(os2.h)
AC_CHECK_LIB(socket, ntohl)

dnl
dnl Checks for GTK+ version, use this
dnl =================================
AM_PATH_GTK(1.0.0,
            [LIBS="$LIBS $GTK_LIBS"
            CFLAGS="$CFLAGS $GTK_CFLAGS"],
            AC_MSG_ERROR(Cannot find GTK: Is gtk-config in path?))

gtk_version=`$GTK_CONFIG --version`
gtk_major_version=`echo $gtk_version | awk -F'.' '{print $1}'`
gtk_minor_version=`echo $gtk_version | awk -F'.' '{print $2}'`
gtk_micro_version=`echo $gtk_version | awk -F'.' '{print $3}'`

if test $gtk_major_version -gt 1 -o \
	$gtk_major_version -eq 1 -a $gtk_minor_version -gt 2 -o \
	$gtk_major_version -eq 1 -a $gtk_minor_version -eq 2 -a $gtk_micro_version -ge 5; then
	gtk_stat="up-to-date"
else
	gtk_stat="out-of-date"
fi

dnl
dnl Checks for header files.
dnl ========================
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(unistd.h)

dnl
dnl Checks for typedefs, structures, and compiler characteristics.
dnl ==============================================================
AC_C_CONST
AC_STRUCT_TM

dnl
dnl Checks for library functions.
dnl =============================
AC_CHECK_FUNCS(getcwd)

dnl
dnl Checks for i18n support
dnl =======================
ALL_LINGUAS="zh_CN.EUC fr es de"
AM_GNU_GETTEXT
LIBS="$LIBS $INTLLIBS"
localedir='$(prefix)/$(DATADIRNAME)/locale'
AC_SUBST(localedir)

AC_SUBST(IMCFLAGS)
AC_SUBST(IMLIBS)

AC_OUTPUT(src/Makefile Makefile intl/Makefile po/Makefile.in man/Makefile)

dnl
dnl Tell you what optional libraries you've installed
dnl =================================================

echo
echo "GTK See configure result:"
echo
echo "JPEG format support: $jpeg_support"
echo "TIFF format support: $tiff_support"
echo "PNG format support:  $png_support"
echo
echo "GTK+ version: $gtk_version ($gtk_stat)"
echo "==============================================================="
echo "Type \"make\" to compile gtksee."
echo

